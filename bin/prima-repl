#!/usr/bin/env perl
use strict;
use warnings;

use Carp;
use Getopt::Long qw( :config no_ignore_case pass_through );
use Pod::Usage;

BEGIN { # Run this before Prima tries to get X
    my @include_dirs;
    my @include_modules;
    GetOptions(
        'h|help' => \( my $help ),
        'I:s' => \@include_dirs,
        'M:s' => \@include_modules,
    );
    unshift @INC, $_ for @include_dirs, map { $1 if /^-I(.+)/ } @ARGV;
    for my $mod ( @include_modules, map { $1 if /^-M(.+)/ } @ARGV ) {
        next unless $mod;
        my ( $module, $import ) = split /=/, $mod;
        my @import = $import ? ( split /,/, $import ) : ();
        # The jiggery-pokery of require $var/import is too much
        eval "use $module" . ( @import ? " qw(" . join( " ", @import ) . ");" : ";" );
        die "Error '$@' when loading module '$module'" if $@;
    }
    @ARGV = grep { !/^-[IM]/ } @ARGV;
    pod2usage(0) if $help;
};

#use Eval::WithLexicals;

my $DEBUG_OUTPUT = 0;
my $initrc_filename = $ARGV[0] || 'prima-repl.initrc';
# Load PDL if they have it
my ($loaded_PDL, $loaded_Prima_Graphics);
BEGIN {
	$loaded_PDL = 0;
	eval {
		require PDL;
		PDL->import;
		require PDL::NiceSlice;
		$loaded_PDL = 1;
	};
	print $@ if $@ and $@ !~ /^Can't locate/;
	
	# Load PDL::Graphics::Prima if they have it
	$loaded_Prima_Graphics = 0;
	eval {
		require PDL::Graphics::Prima;
		PDL::Graphics::Prima->import;
		require PDL::Graphics::Prima::Simple;
		PDL::Graphics::Prima::Simple->import;
		$loaded_Prima_Graphics = 1;
	};
	print $@ if $@ and $@ !~ /^Can't locate/;

	# This function provides access to the original stdout file handle
	my $stdout = \*STDOUT;
	*App::Prima::REPL::print_to_terminal = sub {
		print $stdout @_;
	};
}

#########################
# Main Application Code #
#########################

use App::Prima::REPL;
use App::Prima::REPL::Commands;

my $repl = App::Prima::REPL->new( 
  default_help_page => 'App::Prima::REPL::Help',
  has_PDL => $loaded_PDL,
  has_Prima_Graphics => $loaded_Prima_Graphics,
  debug_output => $DEBUG_OUTPUT,
);
my $commands = App::Prima::REPL::Commands->new( repl => $repl );
$commands->alias_functions;

################################
# Output handling and mangling #
################################

# Set autoflush on stdout:
$|++;

###############################
# Tie STDOUT to Output window #
###############################

use App::Prima::REPL::IO::OutWindow;

# Create the tied file handle that we will reassign
tie *PRINTOUT, 'App::Prima::REPL::IO::OutWindow' => $repl;
# Redirect standard output to the new tied file handle
select( *PRINTOUT );

############################################
# Tie STDERR to Output window and terminal #
############################################

# Create the tied file handle
tie *ERROUT, 'App::Prima::REPL::IO::OutWindow::Err' => $repl;
# Tie stderr to the new tied file handle
*main::STDERR = \*ERROUT;

###############
# Final items #
###############

eval 'require PDL::Version' if not defined $PDL::Version::VERSION;

# Print the opening message:
print "Welcome to the Prima REPL, version $App::Prima::REPL::VERSION.\n";
print "Using PDL version $PDL::Version::VERSION\n" if ($loaded_PDL);
print "Using PDL::Graphics::Prima\n" if ($loaded_Prima_Graphics);
print "\n";
print join(' ', "If you don't know what you're doing, you can get help by"
				, "typing 'help' and pressing Enter, or by pressing Ctrl-h.\n");


#################################
# Run any initialization script #
#################################
sub redo_initrc {
	my $filename = $initrc_filename if -f $initrc_filename;
	$filename = "$initrc_filename.pl" if -f "$initrc_filename.pl";
	if ($filename) {
		print "Running initialization script\n";
		# Load the init script and send it to 
		open my $fh, '<', $filename;
		my $text = do { local( $/ ) ; <$fh> };
		my_eval("#line 1 \"$filename\"\n$text");
		$repl->warn("Errors encountered running the initialization script:\n$@\n")
			if $@;
		$@ = '';
	}
	else {
		print "No initialization script found\n";
	}
}
redo_initrc if -f $initrc_filename or -f "$initrc_filename.pl";

$repl->run; # Prima->run
# Remove the logfile. This will not happen with a system failure, which means
# that the logfile is 'saved' only when there was a problem. The special case of
# the user typing 'exit' at the prompt is handled in pressed_enter().
unlink $repl->logfile;

__END__

=head1 NAME

prima-repl - A Perl REPL using Prima

=head1 SYNOPSIS

    prima-repl [-I <lib_dir>] [-M <module>] [<init_file>]
    prima-repl -h

=head1 DESCRIPTION

Open a Prima REPL window.

=head1 ARGUMENTS

=head2 init_file

Specify the name of a Perl file to run to initialize the REPL window. This file can open
new tabs, add widgets, or more. See L<App::Prima::REPL::Help> "RC File and notes".

=head1 OPTIONS

=head2 -I <lib_directory>

Add a directory to find modules in.

=head2 -M <module>

Load a module at startup. Imports can be added using MyMod=foo,bar,baz to import the
symbols "foo", "bar", and "baz" from the module MyMod.

=head2 -h|--help

Show this help file.

=head1 SEE ALSO

=over 4

=item L<App::Prima::REPL::Help>

=back

=head1 Author, Repository

This program is Copyright David Mertens, 2011, 2012. It is distributed
under the same terms as Perl itself.

The latest copy of this project should be available on Github at
L<https://github.com/run4flat/App-Prima-REPL>.

=cut
